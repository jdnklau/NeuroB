subprojects {
    apply plugin: 'java'
    apply plugin: 'idea'

    project.version = '0.47.0-SNAPSHOT'
    project.group = 'de.hhu.stups'

    project.sourceCompatibility = '1.8'
    project.targetCompatibility = '1.8'

    repositories {
        mavenCentral()
        maven {
            name "sonatype snapshots"
            url "https://oss.sonatype.org/content/repositories/snapshots"
        }
        maven {
            name "sonatype releases"
            url "https://oss.sonatype.org/content/repositories/releases"
        }
    }

    sourceSets {
        integrationTest {
            java.srcDir file('src/it/java')
            resources.srcDir file('src/it/resources')
            compileClasspath += main.output + test.output
            runtimeClasspath += main.output + test.output
        }
    }

    idea {
        module {
            testSourceDirs += project.sourceSets.integrationTest.java.srcDirs
            testSourceDirs += project.sourceSets.integrationTest.resources.srcDirs
        }
    }

    dependencies {
        // ProB dependencies
        compile(group: 'de.hhu.stups', name: 'de.prob2.kernel', version: '3.2.11-SNAPSHOT')

        // DeepLearning4J dependencies
        compile 'org.deeplearning4j:deeplearning4j-core:1.0.0-beta'

        // for logging
        compile 'org.slf4j:slf4j-api:1.7.21'
        compile group: 'ch.qos.logback', name: 'logback-classic', version: '1.+'

        // for hashing
        compile group: 'commons-codec', name: 'commons-codec', version: '1.11'

        testCompile "org.mockito:mockito-core:2.+"
        testCompile 'org.mockito:mockito-junit-jupiter:2.+'
        testCompile 'org.junit.jupiter:junit-jupiter-api:5.2.0'
        testRuntime 'org.junit.jupiter:junit-jupiter-engine:5.2.0'
    }


    configurations {
        all*.exclude group: 'org.bytedeco', module: 'javacpp-presets'

        integrationTestCompile.extendsFrom(testCompile)
        integrationTestRuntime.extendsFrom(testRuntime)
    }

    task integrationTest(type: Test) {
        description = "Run integration tests"

        testClassesDirs = sourceSets.integrationTest.output.classesDirs
        classpath = sourceSets.integrationTest.runtimeClasspath
        outputs.upToDateWhen { false }

        mustRunAfter(test)
    }

    task wrapper(type: Wrapper) {
        gradleVersion = '4.8'
    }

    test {
        useJUnitPlatform()

        testLogging {
            events "skipped", "failed", "passed"
        }

        reports {
            html.enabled = true
        }
    }

    integrationTest {
        useJUnitPlatform()

        testLogging {
            events "skipped", "failed", "passed"
        }

        reports {
            html.enabled = true
        }

    }

    check.dependsOn(integrationTest)
}
